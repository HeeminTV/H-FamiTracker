@ECHO OFF
SETLOCAL ENABLEDELAYEDEXPANSION

IF "%~8"=="" (
	ECHO Usage : %~nx0 [Waves in decimal]
	ECHO Example :
	ECHO 	%~nx0 0 0 1 1 2 2 3 3
	EXIT /B
)
SET "FIRST_BIN="
SET "SECOND_BIN="
SET "COUNTER=1"
SET "ERRORHAPPENED=0"

CALL :EXT_BLOCK %~1
SET /A "COUNTER+=1"
SHIFT
CALL :EXT_BLOCK %~1
SET /A "COUNTER+=1"
SHIFT
CALL :EXT_BLOCK %~1
SET /A "COUNTER+=1"
SHIFT
CALL :EXT_BLOCK %~1
SET /A "COUNTER+=1"
SHIFT

CALL :EXT_BLOCK %~1
SET /A "COUNTER+=1"
SHIFT
CALL :EXT_BLOCK %~1
SET /A "COUNTER+=1"
SHIFT
CALL :EXT_BLOCK %~1
SET /A "COUNTER+=1"
SHIFT
CALL :EXT_BLOCK %~1
SET /A "COUNTER+=1"
SHIFT

IF !ERRORHAPPENED! EQU 1 EXIT /B

SET "FIRST_HEX="
SET "SECOND_HEX="

CALL :BIN2HEX !FIRST_BIN:~0,4! FIRST_HEX
CALL :BIN2HEX !FIRST_BIN:~4,4! FIRST_HEX

CALL :BIN2HEX !SECOND_BIN:~0,4! SECOND_HEX
CALL :BIN2HEX !SECOND_BIN:~4,4! SECOND_HEX

REM ECHO !FIRST_BIN! !SECOND_BIN!
ECHO 7!FIRST_HEX! V!SECOND_HEX!
EXIT /B

:BIN2HEX 4digits outputvariname
IF "%~1"=="0000" SET "%~2=!%~2!0"
IF "%~1"=="0001" SET "%~2=!%~2!1"
IF "%~1"=="0010" SET "%~2=!%~2!2"
IF "%~1"=="0011" SET "%~2=!%~2!3"

IF "%~1"=="0100" SET "%~2=!%~2!4"
IF "%~1"=="0101" SET "%~2=!%~2!5"
IF "%~1"=="0110" SET "%~2=!%~2!6"
IF "%~1"=="0111" SET "%~2=!%~2!7"

IF "%~1"=="1000" SET "%~2=!%~2!8"
IF "%~1"=="1001" SET "%~2=!%~2!9"
IF "%~1"=="1010" SET "%~2=!%~2!A"
IF "%~1"=="1011" SET "%~2=!%~2!B"

IF "%~1"=="1100" SET "%~2=!%~2!C"
IF "%~1"=="1101" SET "%~2=!%~2!D"
IF "%~1"=="1110" SET "%~2=!%~2!E"
IF "%~1"=="1111" SET "%~2=!%~2!F"
GOTO :EOF

:EXT_BLOCK because_of_how_batch_works._.
SET "BINARY=NaN"
IF "%~1"=="0" SET "BINARY=00"
IF "%~1"=="1" SET "BINARY=01"
IF "%~1"=="2" SET "BINARY=10"
IF "%~1"=="3" SET "BINARY=11"
IF "!BINARY!"=="NaN" (
	ECHO Error : Sample No. !COUNTER! isn't valid.
	SET "ERRORHAPPENED=1"
	GOTO :EOF
)
IF !COUNTER! GTR 4 (
	SET "SECOND_BIN=!SECOND_BIN!!BINARY!"
) ELSE (
	SET "FIRST_BIN=!FIRST_BIN!!BINARY!"
)
GOTO :EOF